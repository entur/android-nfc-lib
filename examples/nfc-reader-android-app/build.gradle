plugins {
    id 'com.android.application'
}

android {
    namespace 'no.entur.abt.nfc.example'
    compileSdk 34

    defaultConfig {
        applicationId "no.entur.abt.nfc.example"
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
    }

    packagingOptions {
        exclude 'META-INF/INDEX.LIST'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.md'
        exclude 'META-INF/LICENSE-notice.md'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LICENSE-notice.md'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/NOTICE.md'
        exclude 'META-INF/io.netty.versions.properties'
    }

}

dependencies {

    components.all(LifecycleAlignmentRule)

    implementation project(":nfc:wrapper-utils")
    implementation project(":nfc:external-acs")
    implementation project(":nfc:external-websocket")
    implementation project(":nfc:external-hid")

    implementation "com.github.tony19:logback-android:${logbackAndroidVersion}"

    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    implementation "androidx.preference:preference:1.2.1"

    androidTestImplementation "junit:junit:${unitVersion}"

    androidTestImplementation("com.adevinta.android:barista:${baristaVersion}") {
        exclude group: "com.google.protobuf", module: "protobuf-lite"
    }
    androidTestImplementation "androidx.test.espresso:espresso-idling-resource:${androidxEspressoVersion}"
    androidTestImplementation "androidx.test.espresso.idling:idling-concurrent:${androidxEspressoVersion}"
    androidTestImplementation "androidx.test:rules:${androidxTestRulesVersion}"
    // UiAutomator Testing
    androidTestImplementation("androidx.test.ext:junit:${androidxTestExtJunitVersion}")

    androidTestImplementation "org.mockito:mockito-core:${mockitoVersion}"
    androidTestImplementation "org.mockito:mockito-android:${mockitoVersion}"
    androidTestImplementation "net.bytebuddy:byte-buddy-android:${byteBuddyVersion}"
    androidTestImplementation "junit:junit:${unitVersion}"
    androidTestImplementation "com.google.truth:truth:${googleTruthVersion}"

    androidTestImplementation project(":nfc:wrapper-test")
    androidTestImplementation project(":nfc:external-test")
    androidTestImplementation project(":nfc:external-hid-test")

    modules {
        module("org.checkerframework:checker-qual") {
            replacedBy("org.checkerframework:checker")
        }
    }

}

abstract class LifecycleAlignmentRule implements ComponentMetadataRule {
    void execute(ComponentMetadataContext ctx) {
        ctx.details.with {
            if (id.group.startsWith("androidx.lifecycle")) {
                belongsTo("no.entur.:lifecycle-virtual-platform:${id.version}")
            }
        }
    }
}
